@rem
@rem $Id$
@rem

@echo off

rem ---------------------------------------------------------------
rem Copyright 2009 Viktor Szakats (harbour.01 syenar.hu)
rem See COPYING for licensing terms.
rem
rem Script run after Harbour make install to finish install process
rem (for Windows/DOS)
rem
rem Contains: install package creator script, which requires:
rem
rem    - Info-ZIP zip.exe in PATH
rem         or HB_DIR_ZIP envvar set to its dir with an ending backslash.
rem         https://sourceforge.net/project/showfiles.php?group_id=118012
rem    - NullSoft Installer installed (NSIS)
rem         https://sourceforge.net/project/showfiles.php?group_id=22049&package_id=15374
rem      (only for Windows builds)
rem    - makensis.exe (part of NSIS) in PATH
rem         or HB_DIR_NSIS envvar set to its dir with an ending backslash.
rem      (only for Windows builds)
rem ---------------------------------------------------------------

if "%HB_BIN_INSTALL%" == "" echo ! HB_BIN_INSTALL needs to be set.
if "%HB_BIN_INSTALL%" == "" goto END

echo ! Making %HB_BIN_INSTALL%\hbmk.cfg...
echo # hbmk2 configuration> %HB_BIN_INSTALL%\hbmk.cfg
echo # Generated by Harbour build process>> %HB_BIN_INSTALL%\hbmk.cfg
echo.>> %HB_BIN_INSTALL%\hbmk.cfg
echo libpaths=../contrib/%%{hb_name}>> %HB_BIN_INSTALL%\hbmk.cfg
echo libpaths=../contrib/rddsql/%%{hb_name}>> %HB_BIN_INSTALL%\hbmk.cfg
echo libpaths=../addons/%%{hb_name}>> %HB_BIN_INSTALL%\hbmk.cfg
echo libpaths=../examples/%%{hb_name}>> %HB_BIN_INSTALL%\hbmk.cfg

if "%HB_SHELL%" == "nt" goto _SH_NT

   if "%HB_INSTALL_PREFIX%" == "" goto END

   copy CHANG*     %HB_INSTALL_PREFIX%\CHANGES > nul
   copy COPYING    %HB_INSTALL_PREFIX% > nul
   copy ERRATA     %HB_INSTALL_PREFIX% > nul
   copy INSTALL    %HB_INSTALL_PREFIX% > nul
   copy TODO       %HB_INSTALL_PREFIX% > nul

   goto END

:_SH_NT

   if "%HB_INSTALL_PREFIX%" == "" goto _NO_COPY

   if     "%HB_PLATFORM%" == "dos" copy ChangeLog* "%HB_INSTALL_PREFIX%\CHANGES" > nul
   if not "%HB_PLATFORM%" == "dos" copy ChangeLog* "%HB_INSTALL_PREFIX%\" > nul

   copy COPYING    "%HB_INSTALL_PREFIX%\" > nul
   copy ERRATA     "%HB_INSTALL_PREFIX%\" > nul
   copy INSTALL    "%HB_INSTALL_PREFIX%\" > nul
   copy TODO       "%HB_INSTALL_PREFIX%\" > nul

:_NO_COPY

   if "%HB_PLATFORM%" == "dos" goto _NO_DLL_BIN
   if "%HB_PLATFORM%" == "linux" goto _NO_DLL_BIN
   if "%HB_BUILD_DLL%" == "no" goto _NO_DLL_BIN

   echo ! Making shared version of Harbour binaries...
   "%HB_HOST_BIN_DIR%\hbmk2" -quiet -q0 -lng=en-EN -shared "-o%HB_BIN_INSTALL%\hbrun-dll"    "%HB_TOP%\utils\hbrun\hbrun.hbp"
   "%HB_HOST_BIN_DIR%\hbmk2" -quiet -q0 -lng=en-EN -shared "-o%HB_BIN_INSTALL%\hbmk2-dll"    "%HB_TOP%\utils\hbmk2\hbmk2.hbp"
   "%HB_HOST_BIN_DIR%\hbmk2" -quiet -q0 -lng=en-EN -shared "-o%HB_BIN_INSTALL%\hbtest-dll"   "%HB_TOP%\utils\hbtest\hbtest.hbp"
   "%HB_HOST_BIN_DIR%\hbmk2" -quiet -q0 -lng=en-EN -shared "-o%HB_BIN_INSTALL%\hbi18n-dll"   "%HB_TOP%\utils\hbi18n\hbi18n.hbp"
   "%HB_HOST_BIN_DIR%\hbmk2" -quiet -q0 -lng=en-EN -shared "-o%HB_BIN_INSTALL%\hbformat-dll" "%HB_TOP%\utils\hbformat\hbformat.hbp"

:_NO_DLL_BIN

   if "%HB_PLATFORM%" == "dos" goto _NO_ICON_BIN
   if "%HB_PLATFORM%" == "linux" goto _NO_ICON_BIN

   rem ; We build this here, because GNU Make wouldn't add the icon.
   echo ! Making hbrun with application icon...
   "%HB_HOST_BIN_DIR%\hbmk2" -quiet -q0 -lng=en-EN "-o%HB_BIN_INSTALL%\hbrun" "%HB_TOP%\utils\hbrun\hbrun.hbp"

:_NO_ICON_BIN

   if "%HB_BUILD_IMPLIB%" == "yes" call "%HB_TOP%\bin\hb-mkimp.bat"

:_NO_IMPLIB

   if "%HB_PLATFORM%" == "linux" goto _NO_PKG
   if not "%HB_BUILD_PKG%" == "yes" goto _NO_PKG

   rem NOTE: Believe it or not this is the official method to zip a different dir with subdirs
   rem       without including the whole root path in filenames; you have to 'cd' into it.
   rem       Even with zip 3.0. For this reason this can't work with plain MS-DOS and we need
   rem       absolute path in HB_TOP. There is also no zip 2.x compatible way to force creation
   rem       of a new .zip, so we have to delete it first to avoid mixing in an existing .zip
   rem       files. [vszakats]

   echo ! Making Harbour .zip install package: '%HB_TOP%\%HB_PKGNAME%.zip'
   if exist "%HB_TOP%\%HB_PKGNAME%.zip" del "%HB_TOP%\%HB_PKGNAME%.zip"
   pushd
   cd "%HB_INSTALL_PREFIX%"
   cd ..
   "%HB_DIR_ZIP%zip.exe" -q -9 -X -r -o "%HB_TOP%\%HB_PKGNAME%.zip" . -i "%HB_PKGNAME%\*" -x *.tds -x *.exp
   popd

   echo ! Making Harbour .exe install package: '%HB_TOP%\%HB_PKGNAME%.exe'
   "%HB_DIR_NSIS%makensis.exe" /V2 "%HB_TOP%\package\mpkg_win.nsi"

:_NO_PKG

:END
